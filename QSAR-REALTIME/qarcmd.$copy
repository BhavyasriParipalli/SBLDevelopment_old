 % QARCMD: MACRO;
 /********************************************************************/
 /* UPDATE LOG:                                                      */
 /* ----------                                                       */
 /* SR 0253  REMOVE FUNCTIONS RELATED TO DBDQSAR DATA BASE           */
 /*                                                        9NSI SPZ  */
 /* SR 00066 list QSARs with a certain problem number      0NSK lbs  */
 /********************************************************************/
 
 DCL CTAB(255) CHAR;
 DCL TBENT CHAR;
 DCL I FIXED;
 DCL J FIXED;
 J=0;
 @SPACE (2);
 
 /********************************************************************/
 /*  THE FOLLOWING ARE THE COMMANDS AND PHASES FOR EACH COMMAND.     */
 /********************************************************************/
 /*    EACH ELEMENT IN THE ARRAY DESCRIBES A COMMAND OR CMD PHASE.   */
 /*    THE FIRST TWO BYTES IS THE COMMAND ABBREVIATION.              */
 /*    THE SECOND TWO BYTES IS THE PHASE NUMBER (FUNCTION COMMANDS   */
 /*      ARE PHASE 00).                                              */
 /*    THE THIRD TWO BYTES ARE THE COMMAND POSITION WITHIN THE LIST. */
 /*    THE NEXT 28 BYTES IS FOR THE MENU DISPLAY.                    */
 /********************************************************************/
 @SPACE ;
 CTAB(1)='MR0002Multiple Resubmit         '  ;
 CTAB(2)='QE0003QSAR Entry                '  ;
 CTAB(3)='RS0004Record Selection          '  ;
 CTAB(4)='VI0005View/Inquiry              '  ;
 CTAB(5)='CP0006Common Problem Number     '  ;
        /********************************************/
        /*  BUILD THE APPLICATION COMMAND TABLE     */
        /*  FOR INCLUSION IN THE RAMS SYSTEM.       */
        /********************************************/
 ANS('DCL          /* QSAR COMMAND TABLE            */ ') COL(2);
 ANS('1 QARCMDTB STATIC LOCAL,') COL(4);
 DO J=1 TO 255 WHILE CTAB(J)^='';  /* 255 OR END OF ENTRIES    */
 END;
 J=J-1;                /*ADJUST COUNT*/
 @SPACE 2;
 /********************************************************************/
 /*  INITIALIZE THE TABLE HEADER INFORMATION                         */
 /********************************************************************/
 @SPACE ;
 ANS('2 QARHDR,                   /* CMD TABLE HEADER */ ') COL(6);
 ANS('3 QARFLGS  BIT(8) INIT(''10000000''B), /* FLAG BYTE */ ')
    COL(8) ;
 ANS('         /*  BIT(0)   APPL USES APPL COMMANDS   */ ') COL(8);
 ANS('         /*  BIT(1)   APPL PROF HAS UNIQUE DATA */ ') COL(8);
 ANS('         /*  BIT(2)   APPL HAS PF KEY DATA      */ ') COL(8);
 ANS('         /*  BIT(3)   PROFILE HAS BRANCH OFFICE */ ') COL(8);
 ANS('         /*  BIT(4-7) RESERVED                  */ ') COL(8);
 ANS('3 QARNTS FIXED(8) INIT('||CHAR(J)||'), /* NBR ENT */ ')
     COL(8) ;
 ANS('3 * CHAR(2), /* FILLER */ ') COL(8);
 @SPACE 2;
 /********************************************************************/
 /*  BUILD ONE ENTRY FOR EACH COMMAND                                */
 /*    3 CMDFF,                                     /* FF COMMAND    */
 /*      5 FFPP CHAR(8) INIT('FF',                  /* ROUTINE NAME  */
 /*      5 * CHAR(4) INIT('ACFF'),                  /* TRANSACTION ID*/
 /*      5 * CHAR(4) ,                              /* RESERVED      */
 /*      5 * BIT(8) INIT('00000000'B),              /* COMMAND TYPE  */
 /*      5 * CHAR(28),                              /*  USER AREA    */
 /*        7 * CHAR(26) INIT('EXPLANATION'),        /*  USER AREA    */
 /*        7 * CHAR(2)                              /*  USER AREA    */
 /********************************************************************/
 @SPACE ;
 DO I=1 TO 255 WHILE CTAB(I)^='';
   ANS('2 '||'CMD'||CTAB(I,1:4)||',                  /*'||
            CTAB(I,1:2)||' COMMAND     */')     COL(6) ;
   ANS('3 CMD'||CTAB(I,1:2)||' CHAR(8) INIT('''||CTAB(I,1:2)||'''),
            /* COMMAND */') COL(8);
   ANS('3 '||CTAB(I,1:2)||'EPNAME CHAR(8) INIT(''PQAR'||
            CTAB(I,1:2)||'''), /* MODULE NAME */') COL(8);
   ANS('3 '||CTAB(I,1:2)||'TRANID'||' CHAR(4)'||
               ' INIT(''SRMM''), /*TRAN ID*/') COL(8);
   ANS('3 * CHAR(4),                           /* RESERVED */ ')
           COL(8) ;
   ANS('3 '||CTAB(I,1:2)||'CMDTYP'||
              ' BIT(8) INIT(''00000000''B),    /* COMMAND TYPE */ ')
           COL(8) ;
   ANS('3 '||CTAB(I,1:2)||'OFFSET'||
              ' BIN(8) INIT('||CTAB(I,5:6)||'), /* AUTH BYTE OFFSET*/')
          COL(8) ;
   ANS('3 '||CTAB(I,1:2)||'DATA  CHAR(28) ,'||
                       '/*  USER AREA        */ ')  COL(8);
   ANS('4 '||CTAB(I,1:2)||'DATA1 CHAR(26) '||
                       'INIT('''||CTAB(I,7:32)||'''), /*USER AREA*/ ')
          COL(10) ;
   ANS('4 * CHAR(2)    , /*FILL*/ ') COL(10);
 END;
 ANS('2 QARTBND CHAR(0) ;  /* END OF COMMAND TABLE */ ') COL(6);
 ANS(' @EJECT;') COL(2);
 % END QARCMD;
